apply plugin: 'maven'

ext.optionalDeps = []
ext.providedDeps = []

ext.optional = { optionalDeps << it }
ext.provided = { providedDeps << it }

install {
  repositories.mavenInstaller {
    customizePom(pom, project)
  }
}

def customizePom(pom, gradleProject) {
  pom.whenConfigured { generatedPom ->
    // respect 'optional' and 'provided' dependencies
    gradleProject.optionalDeps.each { dep ->
      generatedPom.dependencies.find { it.artifactId == dep.name }?.optional = true
    }
    gradleProject.providedDeps.each { dep ->
      generatedPom.dependencies.find { it.artifactId == dep.name }?.scope = 'provided'
    }

    // eliminate test-scoped dependencies (no need in maven central poms)
    generatedPom.dependencies.removeAll { dep ->
      dep.scope == 'test'
    }

    // add all items necessary for maven central publication
    generatedPom.project {
      name = "Spring Data REST"
      description = "Directly export Spring Data-managed PersistentEntities to the web."
      url = 'http://github.com/SpringSource/spring-data-rest'
      organization {
        name = 'SpringSource'
        url = 'http://www.springsource.org/spring-data/rest'
      }
      licenses {
        license {
          name 'The Apache Software License, Version 2.0'
          url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          distribution 'repo'
        }
      }
      scm {
        url = 'http://github.com/SpringSource/spring-data-rest'
        connection = 'scm:git:git://github.com/SpringSource/spring-data-rest'
        developerConnection = 'scm:git:git://github.com/SpringSource/spring-data-rest'
      }
      developers {
        developer {
          id = 'jbrisbin'
          name = 'Jon Brisbin'
          email = 'jbrisbin@vmware.com'
        }
        developer {
          id = 'ogierke'
          name = 'Oliver Gierke'
          email = 'ogierke@vmware.com'
        }
      }
    }
  }
}