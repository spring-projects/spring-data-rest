apply plugin: "base"

allprojects {
  apply plugin: "idea"
  apply plugin: "maven"

  group = "org.springframework.data"
  version = "$sdRestVersion"

  configurations.all {
    exclude group: "commons-logging"
    exclude module: "slf4j-log4j12"
    exclude module: "groovy-all", version: "1.8.0-beta-3-SNAPSHOT"
  }

  repositories {
    maven { url "http://repo.springsource.org/libs-snapshot" }
    maven { url "http://repo.springsource.org/libs-milestone" }
    maven { url "http://repo.springsource.org/libs-release" }
  }

}

configure(subprojects) { subproject ->
  apply plugin: "java"
  apply plugin: "groovy"

  [compileJava, compileTestJava]*.options*.debugOptions*.debugLevel = "source,lines,vars"
  [compileJava, compileTestJava]*.options*.compilerArgs = ["-Xlint:-serial", "-Xlint:-unchecked"]

  project.sourceCompatibility = 1.6
  project.targetCompatibility = 1.6

  javadoc {
    options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
    options.author = true
    options.header = subproject.name
    //options.overview = "${projectDir}/src/main/java/overview.html"
  }

  task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allJava
  }

  task javadocJar(type: Jar) {
    classifier = "javadoc"
    from javadoc
  }

  artifacts {
    archives sourcesJar
    archives javadocJar
  }

  dependencies {
    groovy "org.codehaus.groovy:groovy:$groovyVersion"

    // Logging
    compile "org.slf4j:slf4j-api:$slf4jVersion"
    runtime "org.slf4j:jcl-over-slf4j:$slf4jVersion"
    runtime "ch.qos.logback:logback-classic:$logbackVersion"

    // Jackson JSON
    compile "org.codehaus.jackson:jackson-mapper-asl:$jacksonVersion"

    // Spring
    compile "org.springframework:spring-beans:$springVersion"
    compile "org.springframework:spring-context:$springVersion"
    compile "org.springframework:spring-core:$springVersion"
    compile "org.springframework:spring-orm:$springVersion"
    compile "org.springframework:spring-web:$springVersion"

    // Testing
    testCompile "org.spockframework:spock-core:$spockVersion"
    testCompile "org.spockframework:spock-spring:$spockVersion"
    testCompile "org.hamcrest:hamcrest-all:1.1"
    testCompile "org.springframework:spring-test:$springVersion"
    testRuntime "org.springframework:spring-context-support:$springVersion"

  }

}

task wrapper(type: Wrapper) { gradleVersion = "1.0-rc-1" }

idea {
  module {
    inheritOutputDirs = false
    outputDir = file("build/classes/main")
    testOutputDir = file("build/classes/test")
    downloadJavadoc = false
    downloadSources = true
  }
  project {
    ipr {
      withXml { xml ->
        xml.node.component.find { it.@name == "VcsDirectoryMappings" }.mapping.@vcs = "Git"
        xml.node.component.find { it.@name == "ProjectRootManager" }.output.@url = "file://\$PROJECT_DIR\$/build"
      }
    }
  }
}